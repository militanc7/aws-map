#!/usr/bin/env perl

use AWS::Network::SecurityGroupMap;
use Mojolicious::Lite;
use Image::Size;

my $region = shift @ARGV or die "Please specify region";
@ARGV = ('daemon');

my $map = AWS::Network::SecurityGroupMap->new(
  region => $region, 
);

$map->scan;
$map->draw;
$map->graphviz->run(format => 'png');
my $image = $map->graphviz->dot_output;
my ($image_width, $image_height) = imgsize(\$image);

get '/graph.png' => sub {
  my $c = shift;
  $c->res->headers->content_type('image/png');
  $c->render(data => $image);
};

get '/' => sub { 
  my $c = shift;
  $c->stash(image_width => $image_width);
  $c->stash(image_height => $image_height);
  $c->render(template => 'index');
};

app->start;

__DATA__
@@ index.html.ep
<html>
  <head>
    <link rel="stylesheet" href="https://unpkg.com/leaflet@1.3.1/dist/leaflet.css"
          integrity="sha512-Rksm5RenBEKSKFjgI3a41vrjkw4EVPlJ3+OiI65vTjIdo9brlAacEuKOiQ5OFh7cOI1bkDwLqdLw3Zg0cRJAAQ=="
          crossorigin=""
    />
    <style>
    #image-map {
      width: 100%;
      height: 100%;
      border: 1px solid #ccc;
      margin-bottom: 10px;
    }
    </style>
  </head>
  <body>
    <div id="image-map"></div>
    <script src="https://unpkg.com/leaflet@1.3.1/dist/leaflet.js"
            integrity="sha512-/Nsx9X4HebavoBvEBuyp3I7od5tA0UzAxs+j83KgC8PU0kgB4XiK4Lfe4y4cgBtaRJQEIFCW+oC506aPT2L1zw=="
            crossorigin="">
    </script>

    <script>
    var map = L.map('image-map', {
      minZoom: 1,
      maxZoom: 4,
      center: [0, 0],
      zoom: 1,
      crs: L.CRS.Simple
    });

    var w = <%= $image_width %>,
        h = <%= $image_height %>,
        url = '/graph.png';

    // calculate the edges of the image, in coordinate space
    var southWest = map.unproject([0, h], map.getMaxZoom()-1);
    var northEast = map.unproject([w, 0], map.getMaxZoom()-1);
    var bounds = new L.LatLngBounds(southWest, northEast);

    // add the image overlay, 
    // so that it covers the entire map
    L.imageOverlay(url, bounds).addTo(map);

    // tell leaflet that the map is exactly as big as the image
    map.setMaxBounds(bounds);
    </script>

  </body>
</html>
